import groovy.json.JsonSlurper

def sdkVersions = new JsonSlurper().parse file("../sdk-versions.json")

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.5.0")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:2.0.0")
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

// Define helper functions for Kotlin version management
def getKotlinVersion() {
    return rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") : "2.0.0"
}

def getKotlinGradlePluginVersion() {
    return rootProject.ext.has("kotlinGradlePluginVersion") ? rootProject.ext.get("kotlinGradlePluginVersion") : "2.0.0"
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    namespace 'com.reactnativehmssdk'
    compileSdkVersion safeExtGet('Hmssdk_compileSdkVersion', 31)
    defaultConfig {
        minSdkVersion safeExtGet('Hmssdk_minSdkVersion', 21)
        targetSdkVersion safeExtGet('Hmssdk_targetSdkVersion', 31)
        versionCode 1
        versionName "1.0"
    }

    lintOptions {
        disable 'GradleCompatible'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
}

repositories {
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    google()
    mavenCentral()
    gradlePluginPortal( )
    maven { url 'https://jitpack.io' }
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"
    implementation "com.google.code.gson:gson:2.9.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${getKotlinVersion()}"
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.3"
    implementation "androidx.appcompat:appcompat:1.3.1"

    implementation "live.100ms:android-sdk:${sdkVersions["android"]}"
    implementation "live.100ms:video-view:${sdkVersions["android"]}"
    implementation "live.100ms:hls-player:${sdkVersions["android"]}"
    implementation "live.100ms:hms-noise-cancellation-android:${sdkVersions["android"]}"
}

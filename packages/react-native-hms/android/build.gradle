import groovy.json.JsonSlurper

def sdkVersions = new JsonSlurper().parse file("../sdk-versions.json")

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def getKotlinVersion() {
    return safeExtGet("kotlinVersion", "2.0.21")
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace 'com.reactnativehmssdk'
    compileSdkVersion safeExtGet('compileSdkVersion', 35)
    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 24)
        targetSdkVersion safeExtGet('targetSdkVersion', 35)
        versionCode 1
        versionName "1.0"
    }

    buildFeatures {
        buildConfig = true
    }

    lintOptions {
        disable 'GradleCompatible'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    // kotlinOptions removed - let Kotlin inherit JVM target from compileOptions
    // This avoids languageVersion finalization conflict with React Native Gradle Plugin

    // New Architecture: Add codegen source sets
    sourceSets {
        main {
            if (project.ext.has("isNewArchitectureEnabled") && project.ext.isNewArchitectureEnabled) {
                java.srcDirs += [
                    "build/generated/source/codegen/java"
                ]
            }
        }
    }
}

repositories {
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    google()
    mavenCentral()
    gradlePluginPortal( )
    maven { url 'https://jitpack.io' }
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"
    implementation "com.google.code.gson:gson:2.9.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${getKotlinVersion()}"
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.3"
    implementation "androidx.appcompat:appcompat:1.3.1"

    implementation "live.100ms:android-sdk:${sdkVersions["android"]}"
    implementation "live.100ms:video-view:${sdkVersions["android"]}"
    implementation "live.100ms:hls-player:${sdkVersions["android"]}"
    implementation "live.100ms:hms-noise-cancellation-android:${sdkVersions["android"]}"
}

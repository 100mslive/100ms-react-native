import groovy.json.JsonSlurper

def sdkVersions = new JsonSlurper().parse file("../sdk-versions.json")

buildscript {
    ext.kotlin_version = "1.6.0"
    ext.hms_coroutines_version = "1.6.0"
    ext.ktx_version = "1.6.0"
    if (project == rootProject) {
        repositories {
            google()
            mavenCentral()
            gradlePluginPortal( )
        }

        dependencies {
            classpath 'com.android.tools.build:gradle:3.5.3'
        }
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('Hmssdk_compileSdkVersion', 30)
    defaultConfig {
        minSdkVersion safeExtGet('Hmssdk_minSdkVersion', 21)
        targetSdkVersion safeExtGet('Hmssdk_targetSdkVersion', 30)
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled true
        }
    }
    lintOptions {
        disable 'GradleCompatible'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    google()
    mavenCentral()
    gradlePluginPortal( )
    maven { url 'https://jitpack.io' }
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"
    implementation "com.google.code.gson:gson:2.9.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:$ktx_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$hms_coroutines_version"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation ("androidx.appcompat:appcompat:1.3.1")

    // 100ms
    implementation "live.100ms:android-sdk:${sdkVersions["android"]}"
    implementation "live.100ms:video-view:${sdkVersions["android"]}"
    implementation "live.100ms:hls-player:${sdkVersions["android"]}"
    implementation "live.100ms:hls-player-stats:${sdkVersions["android"]}"
}
